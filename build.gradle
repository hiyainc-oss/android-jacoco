plugins {
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.10.1'
  id 'maven-publish'
  id 'groovy'
  id 'jacoco'
}

repositories {
  jcenter()
  google()
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  // TODO available with Gradle 6.0
//  withJavadocJar()
//  withSourcesJar()
}

dependencies {
  testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testImplementation 'com.android.tools.build:gradle:3.5.2'
  testImplementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
}

group = 'com.hiya'
version = "0.1.4"

gradlePlugin {
  plugins {
    jacocoAndroid {
      id = "com.hiya.jacoco-android"
      implementationClass = "com.hiya.plugins.JacocoAndroidPlugin"
    }
  }
}

pluginBundle {
  website = 'https://github.com/autonomousapps/jacoco-android-gradle-plugin'
  vcsUrl = 'https://github.com/autonomousapps/jacoco-android-gradle-plugin'

  description = 'Gradle plugin that creates JaCoCo test reports for Android unit tests'

  plugins {
    jacocoAndroid {
      displayName = 'JaCoCo Plugin for Android'
      tags = ['jacoco', 'android', 'testing', 'coverage']
    }
  }
}

// TODO can replace with above once migrated to Gradle 6
task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from groovydoc
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
      artifact sourcesJar
      artifact groovydocJar
    }
  }
  repositories {
    maven {
      // Publish here when you want to see what it looks like
      name = 'local'
      url = "$buildDir/repo"
    }

    def theUrl = artifactory_contextUrl
    def theUsername = artifactory_user
    def thePassword = artifactory_password
    if (theUrl && theUsername && thePassword) {
      maven {
        name = 'artifactory'
        url = "${artifactory_contextUrl}hiya-libs-ivy-release-local"
        credentials(PasswordCredentials) {
          username = artifactory_user
          password = artifactory_password
        }
      }
    }
  }
}

test {
  testLogging {
    exceptionFormat "full"
  }
}

jacocoTestReport {
  reports {
    html.enabled false
    xml.enabled true
  }
}
